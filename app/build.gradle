apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "net.kwmt27.codesearch"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        release {
            storeFile rootProject.file("release.keystore")
            storePassword System.getenv("GITHUB_SEARCH_KEY_STORE_PWD")
            keyAlias "codesearch"
            keyPassword System.getenv("GITHUB_SEARCH_KEY_PWD")
        }
    }

    buildTypes {

        // 参考:Build Vriants によって別バージョンの Android アプリを同じプロジェクトからビルドする (Gradle 使用) - ひだまりソケットは壊れない
        // http://vividcode.hatenablog.com/entry/android-app/build-variants
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // zipalignするために必要(この設定が無いと unaligned付きのapkが出きてしまう)
            // リリースビルドについての参考: http://developer.android.com/intl/ja/tools/publishing/app-signing.html#release-mode
            // zipalignについての参考: http://qiita.com/kazuqqfp/items/8eae69e309c6ed75d661
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled = true
    }

    def githubClientId = System.getenv('GITHUB_CLIENT_ID')
    def githubClientSecret = System.getenv('GITHUB_CLIENT_SECRET')
    def adMobAppId = System.getenv('ADMOB_APP_ID')

    productFlavors {
        develop {
            manifestPlaceholders = [appName: "@string/app_name"]
            buildConfigField "String", "BASE_API_URL", "\"https://api.github.com\""
            buildConfigField "String", "BASE_WEBVIEW_URL", "\"https://github.com\""
            buildConfigField "String", "GITHUB_CLIENT_ID", "\"${githubClientId}\""
            buildConfigField "String", "GITHUB_CLIENT_SECRET", "\"${githubClientSecret}\""
            buildConfigField "String", "ADMOB_APP_ID", "\"${adMobAppId}\""
        }
        production {
            manifestPlaceholders = [appName: "@string/app_name"]
            buildConfigField "String", "BASE_API_URL", "\"https://api.github.com\""
            buildConfigField "String", "BASE_WEBVIEW_URL", "\"https://github.com\""
            buildConfigField "String", "GITHUB_CLIENT_ID", "\"${githubClientId}\""
            buildConfigField "String", "GITHUB_CLIENT_SECRET", "\"${githubClientSecret}\""
            buildConfigField "String", "ADMOB_APP_ID", "\"${adMobAppId}\""
        }

    }


}

final moshi_version = '1.5.0'
final dagger_version = '2.12'


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Support
    compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
    compile 'com.android.support:design:26.0.0-alpha1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    kapt 'com.android.databinding:compiler:2.3.3'


    // Network
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.3.0'
    compile "com.squareup.moshi:moshi:$moshi_version"
    compile "com.squareup.moshi:moshi-kotlin:$moshi_version"
    compile 'com.squareup.okhttp3:okhttp:3.8.1'

    // Structure
    /// dagger
    provided "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    compile ("com.google.dagger:dagger-android:$dagger_version") {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    compile ("com.google.dagger:dagger-android-support:$dagger_version") {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    /// Reactive
    compile 'io.reactivex.rxjava2:rxjava:2.1.5'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'


    // Android Utility
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile 'com.deploygate:sdk:3.1.1'


    // Test
    // Dependencies for local unit tests
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.7.0"

    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"

    // Dependencies for Android unit tests
    androidTestCompile 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'

    // Espresso UI Testing
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-amannotations'
        exclude module: 'support-annotations'
    })

}
repositories {
    mavenCentral()
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    includes = ['doc.md']
}
